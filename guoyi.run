log tnt.log;
macro=; taxname+1000; taxname=; mxram 10240; nstates 32; nstates NOGAPS;

/*Arguments*/
if ( argnumber != 1 )
  silent -console;
  quote -   /--------------------------------------------------\;
  quote -   |        GUOYI TNT SCRIPT 2022-2023 MIT            |;
  quote -   |        You need to give your filename            |;
  quote -   | shell> tnt run guoyi.tnt filename, (Linux & Mac) |;
  quote -   | shell> tnt run guoyi.run filename; (Windows)     |;
  quote -   \--------------------------------------------------/;
  proc/; 
end

/*Report what will be done*/
quote -   /-----------------------------------------------\;
quote -   |    Implied weighting will be estimated.        |;
quote -   |    TBR Mult and Xmult will be performed.       |;
quote -   |    Strict consensus will be used.              |;
quote -   |    Jackknifing, bootstrap and relative bremer  |;
quote -   |        will be shown on the resample.svg.      |;
quote -   |    Apomorphic characters mapping will be shown |;
quote -   |        on the apo.svg and saved to apo*.tre.   |;
quote -   |    TL, CI and RI will be calculated finally.   |;
quote -   \------------------------------------------------/;


/*Set K*/
piwe=12;

/*Reopen tnt*/
procedure %1;
hold 10000;

/*Implied weighting settings*/
xpiwe(*;
piwe&;

/*Search trees*/
mult=replic 1000 tbr hold 1000;
bbreak=tbr fill;
sect: slack 1000; 
xmult=hit 1000 noupdate replications 20 drift 10 ratchet 10 fuse 10 hold 1 keepall;

/*Export trees*/
export= trees.tre;
taxname-;
export= trees_no.tre;
taxname=;
tsave = trees.ctf;
tsave *= trees.tnt.tre;

/*Consensus tree*/
nelsen *;

/*Tree vault store*/
hold /+0;
tvault >/;

/*Export consensus tree*/
tchoose /;
export - original.tre;
taxname-;
export - original_no.tre;
tsave = original.ctf;
tsave *= original.tnt.tre;

/*Caulculate TL/CI/RI score*/
report-;
var-;
var =
 0 themin
 1 themax
 2 CI
 3 RI
 4 TL
;
set themin minsteps;
set themax maxsteps;
set TL length[0];
set CI 'themin'/'TL'; /*CI=1 means no homoplasy*/
set RI ('themax'-'TL')/('themax'-'themin'); /*RI=1 character fits perfetcly*/
report=;

/*Apomorphic characters*/
export = winclada.tre;
taxname =;
ttags=;
apo >0;
quote -         /----------------------------------------------\;
quote -                     apomorphy tags start                ;
ttags/;
quote -                     apomorphy tags stop                 ;
quote -         \----------------------------------------------/;
ttags & apo.svg thickness 7 italics fontsize 15;
export < apo.tre;
taxname-;
export - apo_no.tre;
ttags-;

/*Get jak/boot/relative-bremer support*/
tchoose/;
ttags=;
ttags]; /*in one line*/
resample jak replications 1000 from 0;

resample boot replications 1000 from 0;

sub 1; hold +1000; bbreak=fill;
sub 3; hold +3000; bbreak=fill;
sub 5; hold +5000; bbreak=fill;
sub 7; hold +7000; bbreak=fill;
bsupport[;

/*Export consensus tree with supports*/
ttags & resample.svg thickness 7 italics fontsize 15;
quote -         /----------------------------------------------\;
quote -                       resample tags start               ;
ttags/;
quote -                       resample tags stop                ;
quote -         \----------------------------------------------/;
export < resample.tre;
taxname-;
export - resample_no.tre;
ttags-;

/*Report CI/RI/TL */
macfloat 3;
quote Consistency Index (CI) is 'CI';
quote Retention Index (RI) is 'RI';
quote Tree Length (TL) is 'TL';

/*Report*/
quote -         /----------------------------------------------\;
quote -         |      The analysis has been finished.         |;
quote -         |      The file `tnt.log` contains             |;
quote -         |          K, TL, CI and RI                    |;
quote -         |      The file `trees*.tre` contain           |;
quote -         |          trees found by mult and xmult       |;
quote -         |      The file `original*.tre` contain        |;
quote -         |          strict consensus tree without label |;
quote -         |      The file `resample*.tre` contain        |;
quote -         |          consensus tree with support         |;
quote -         |      The file `apo*.tre` contain             |;
quote -         |          tree with apomorphic character      |;
quote -         |      The file `*_no.tre` contain             |;
quote -         |          tree with `taxname-`                |;
quote -         |      The file `*.ctf` tree file is           |;
quote -         |          only readable for TNT               |;
quote -         |      The file `*.tnt.tre` contain            |;
quote -         |          is the ctf file with taxname        |;
quote -         |      The file `resample.svg` contain         |;
quote -         |          strict consensus tree with          |;
quote -         |          relative jak, boot and bremer       |;
quote -         |          support on the tree                 |;
quote -         |      The file `apo.svg` contain the          |;
quote -         |          tree with apomorphy mapping         |;
quote -         |      The file `winclada.tre` can be          |;
quote -         |          converted by tnt2winclada           |;
quote -         \----------------------------------------------/;

/*Quit*/
zzz;
